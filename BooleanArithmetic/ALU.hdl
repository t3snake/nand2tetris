// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //zx
    Mux16(a= x, b= false, sel= zx, out= afterZx);

    //nx
    Not16(in= afterZx, out= notAfterZx);
    Mux16(a= afterZx, b= notAfterZx, sel= nx, out= afterNx);

    //zy
    Mux16(a= y, b= false, sel= zy, out= afterZy);

    //zx
    Not16(in= afterZy, out= notAfterZy);
    Mux16(a= afterZy, b= notAfterZy, sel= ny, out= afterNy);

    //f
    Add16(a = afterNx, b = afterNy, out = f0);
    And16(a= afterNx, b= afterNy, out= f1);
    Mux16(a= f1, b= f0, sel= f, out= out1);
    
    //no
    Not16(in= out1, out= nOut1);
    Mux16(a= out1, b= nOut1, sel= no, out= tmpOut);

    //ng
    And16(a= tmpOut, b= true, out[0..14]= others, out[15]=ng );

    //zr
    And16(a= tmpOut, b= true, out[0..7]=half1, out[8..15]=half2 );
    Or8Way(in= half1, out= or1);
    Or8Way(in= half2, out= or2);
    Or(a= or1, b= or2, out= notZr);
    Not(in= notZr, out= zr);

    //out
    And16(a= tmpOut, b= true, out= out);






}